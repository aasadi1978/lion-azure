; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!


; These params have to be updated per app

#define MyAppVersion "LION_VERSION_PLACEHOLDER"
#define MyChanges "LION_CHANGES_PLACEHOLDER"

#define MyAppName "LION"
#define MyLocalAppDir "pyinstaller-output"
#define MyOutputDir GetEnv("LION_APP_ROOT") + "\Release"

#define PyInstallerDistFolderName "lion"
#define PyInstallerAppExeName PyInstallerDistFolderName + ".exe"
#define InstallerOutputBaseFilename "install_" + PyInstallerDistFolderName + " " + MyAppVersion

#define MyAppPublisher "FedEx Express - Road P&E"
#define MyAppAssocName MyAppName + ""
#define MyAppAssocExt ".lion"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

#define MyAppIcon GetEnv("LION_APP_ROOT") + "\app\static\images\lion.ico"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1D1A7722-4BA0-4E8D-BC17-B180CEEC2665}
AppName={#MyAppName} {#MyAppVersion}
AppVersion={#MyAppVersion} 
UpdateUninstallLogAppName=yes
AppVerName={#MyAppName} {#MyAppVersion} 
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppName}
; DefaultDirName={localappdata}\Programs\LION
ChangesAssociations=yes
UninstallDisplayName={#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=lowest
OutputDir={#MyOutputDir}
OutputBaseFilename={#InstallerOutputBaseFilename}
SetupIconFile={#MyAppIcon}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
; This is because my PC and created app (pyinstaller on 64bit python) are both 64 bit.
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: taskbarshortcut; Description: "Create a taskbar shortcut"; GroupDescription: "Additional shortcuts:"


[Code]

var

  AppVersion_String: String;
  CustomMessage: String;

function StopLionApp: Boolean;
var
  ResultCode: Integer;
begin
  if not Exec('taskkill', '/F /IM lion.exe', '', SW_HIDE, ewWaitUntilTerminated, ResultCode) then
  begin
    MsgBox('Error stopping lion.exe. Please close the application manually and then try again.', mbError, MB_OK);
    Result := False;
  end
  else
    Result := True;
end;

function InitializeSetup: Boolean;
begin
  Result := StopLionApp;
end;

procedure InitializeWizard();
begin

  AppVersion_String := '{#MyAppVersion}';
  CustomMessage := '{#MyChanges}';

  MsgBox('LION ' + AppVersion_String + ': ' + CustomMessage, mbInformation, MB_OK);
end;

[Files]
Source: "{#MyLocalAppDir}\{#PyInstallerDistFolderName}\{#PyInstallerAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#MyLocalAppDir}\{#PyInstallerDistFolderName}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; Source: "reboot_me.bat"; DestDir: "{app}"; Flags: ignoreversion
; Source: "lion.ico"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#PyInstallerAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#PyInstallerAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#PyInstallerAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\lion_execute.bat"; IconFilename: "{app}\utilities\lion.ico";
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\lion_execute.bat"; IconFilename: "{app}\utilities\lion.ico"; Tasks: desktopicon
; Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\User Pinned\TaskBar\{#MyAppName}"; Filename: "{app}\lion_execute.bat"; IconFilename: "{app}\utilities\lion.ico"; Tasks: taskbarshortcut



[Run]
Filename: "{app}\{#PyInstallerAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

